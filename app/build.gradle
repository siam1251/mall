buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'


repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    google()
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        versionName "1.6.4"
        versionCode "1"

        minSdkVersion 16
        targetSdkVersion 26
        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true

        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "Boolean", "REPORT_CRASH", 'true'
            buildConfigField "Boolean", "JANRAIN_DEV", 'false'
            buildConfigField "Boolean", "IS_APP_IN_PRODUCTION", "true"
            debuggable false
        }
        debug {
            buildConfigField "Boolean", "JANRAIN_DEV", 'true'
            buildConfigField "Boolean", "REPORT_CRASH", 'false'
            buildConfigField "Boolean", "IS_APP_IN_PRODUCTION", "false"
        }
    }
    lintOptions {
        abortOnError false
    }

    productFlavors.whenObjectAdded { flavor ->
        if (rootProject.hasProperty(flavor.name)) {
            applicationId rootProject.ext.packageName + ".${flavor.name}"

            def flavorData = rootProject.ext[flavor.name]
            flavor.manifestPlaceholders = [
                    'appAuthRedirectScheme': rootProject.ext.redirect_uri_scheme + flavorData.appAuthRedirectScheme
            ]
            flavor.resValue 'string', fieldAppName, flavorData.appName
            flavor.resValue 'string', 'google_client_id', flavorData.appAuthRedirectScheme + rootProject.ext.client_id_scheme
            flavor.resValue 'string', 'google_auth_redirect_uri', rootProject.ext.redirect_uri_scheme + flavorData.appAuthRedirectScheme + ':/oauth2redirect'
            flavor.buildConfigField 'String', fieldNotificationId, "\"${flavor.name}" + '-marketing-cloud-id"'
            flavor.buildConfigField 'String', fieldMall, flavorData.mallName
            flavor.buildConfigField 'Boolean', fieldPush, flavorData.hasPush
            flavor.buildConfigField 'Boolean', fieldParkingPolygon, flavorData.hasParkingPolygon
            flavor.buildConfigField 'Boolean', fieldMovie, flavorData.hasMovie
            flavor.buildConfigField 'Boolean', fieldWhiteFav, flavorData.hasWhiteFav
            flavor.buildConfigField 'Boolean', fieldNearestParkingImg, flavorData.hasNearestParkingImg
            flavor.buildConfigField 'Boolean', fieldBlueDot, flavorData.hasBlueDot
        }

    }
    flavorDimensions  "release"
    productFlavors {
        bayshore {}
        crossironmills {}
        eatoncentre {}
        metropolis {}
        //seniondemo {}
        tsawwassenmills {}
        vaughanmills {}
        referencemall {}
    }

}

//Upgrading android studio to 2.3, it was required to add multidex (com.android.support:multidex:1.0.1) as dependency.
//After adding multidex dependency it complained about renderscript dependency being at 25.0.2 and that all com.android.support libraries must use the exact same version specification
//seems like this is currently a bug https://code.google.com/p/android/issues/detail?id=236471

dependencies {
    compile project(':Jump')
    compile "com.google.android.gms:play-services-location:${playServicesGcm}"
    compile "com.google.android.gms:play-services-gcm:${playServicesGcm}"
    compile "com.google.android.gms:play-services-maps:${playServicesGcm}"
    compile "com.google.firebase:firebase-core:${firebase}"
    compile "com.google.firebase:firebase-ads:${firebase}"
    compile "com.google.firebase:firebase-messaging:${firebase}"
    compile "com.google.firebase:firebase-crash:${firebase}"
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile project(path: ':kcpandroidsdk')
    compile files('libs/twitter4j-core-4.0.4.jar')
    compile "com.android.support:appcompat-v7:${supportLibrary}"
    compile "com.android.support:design:${supportLibrary}"
    compile "com.android.support:support-v4:${supportLibrary}"
    compile "com.android.support:preference-v7:${supportLibrary}"
    compile "com.android.support:preference-v14:${supportLibrary}"
    compile "com.android.support:multidex:${multiDex}"
    compile "com.github.bumptech.glide:glide:${glide}"
    compile "com.android.support:cardview-v7:${supportLibrary}"
    compile 'org.jbundle.util.osgi.wrapped:org.jbundle.util.osgi.wrapped.org.apache.http.client:4.1.2'
    compile "com.android.support:recyclerview-v7:${supportLibrary}"
    compile "com.mappedin.sdk:mappedin:${mappedIn}"
    compile "com.google.android.gms:play-services-tagmanager:${firebase}"
    compile("com.crashlytics.sdk.android:crashlytics:2.6.8@aar") {
        transitive = true
    }
    compile("com.twitter.sdk.android:twitter:${twitter}") {
        transitive = true
    }

    compile "org.msgpack:msgpack-core:${msgPack}"
    compile ("com.salesforce.marketingcloud:marketingcloudsdk:${exactTarget}") {
        exclude module: "${salesForceBeaconLib}"
    }
    compile "com.jakewharton:butterknife:${butterKnife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnife}"
}

apply plugin: 'com.google.gms.google-services'